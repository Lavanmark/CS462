ruleset wovyn_base {
  meta {
    use module cs462_keys
    use module twilio_lab alias twilio
       with account_sid = keys:twilio{"account_sid"}
       auth_token = keys:twilio{"auth_token"}
       
    shares __testing
  }
  
  global {
    alert_phone_number = "+19199739210"
    //ent:temperature_threshold = 70.0

    __testing = { "queries":
      [] , 
      "events":
        [ 
          { "domain": "wovyn", "type": "adjust_threshold", "attrs": [ "new_threshold" ] }
        ]
    }
    
    trial = function(){
      ent:temperature_threshold
    }
  }
  
  rule process_heartbeat {
    select when wovyn heartbeat where event:attr("genericThing")
    pre {
      details = event:attrs().klog("attrs")
      temperature_data = event:attr("genericThing"){"data"}{"temperature"}.klog("temp details")
      timestamp = time:now()
    }
    send_directive("process_heartbeat", {"genericThing": "Heartbeat Received."})
    always{
      raise wovyn event "new_temperature_reading"
        attributes { "temperature": temperature_data[0], "timestamp": timestamp }
    }
  }
  
  rule find_high_temps {
    select when wovyn new_temperature_reading
    pre{
      details = event:attrs().klog("temp attr ")
      tempF = event:attr("temperature"){"temperatureF"}.klog("Temp F = ")
    }
    send_directive("find_high_temps", {"threshold_violation": ((tempF > ent:temperature_threshold) => "YES" | "NO")})
    always{
      raise wovyn event "threshold_violation"
        attributes event:attrs
          if (tempF > ent:temperature_threshold)
    }
  }
  
  rule threshold_notification {
    select when wovyn threshold_violation
    pre{
      tempF = event:attr("temperature"){"temperatureF"}
      time = event:attr("timestamp")
    }
    every{
      twilio:default_sender(
            to = alert_phone_number,
            body = ("The temperature reading (" + tempF + ") at " + time 
                  + " was in violation of your threshold of: " 
                  + ent:temperature_threshold + ". <3 KRL")
                      ) setting (response);
      send_directive("twilio_response", {"response": response})
    }
  }
  
  rule adjust_threshold {
    select when wovyn adjust_threshold
    pre{
      new_threshold = event:attr("new_threshold")
    }
    send_directive("adjust_threshold", {"Adjusted threshold to" : new_threshold})
    always{
      ent:temperature_threshold := new_threshold
    }
  }
  
}
